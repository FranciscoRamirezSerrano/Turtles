plugins {
    id 'java'
    id "de.undercouch.download" version "3.4.3"
}

group 'wakingstones'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    maven {
        name = 'gradle-plugins'
        url = 'https://plugins.gradle.org/m2'
    }
    maven {
        name = 'sponge'
        url = 'https://repo.spongepowered.org/maven'
    }
    maven {
        name = 'forge'
        url = 'http://files.minecraftforge.net/maven'
    }
    maven {
        name = 'minecraft'
        url = 'https://libraries.minecraft.net/'
    }
    mavenCentral()
    flatDir {
        dirs "libs"
    }
}

dependencies {
    compile fileTree(dir: 'lib', include: ['*.jar', '*.cfg'], exclude: [
            "gson-2.2.3.jar",
            "guava-15.0.jar"
    ])
    compile 'org.spongepowered:mixin:0.7.10-SNAPSHOT'
    compile 'net.minecraft:launchwrapper:1.11'
    //runtime 'org.apache.logging.log4j:log4j-slf4j-impl:2.8.1'
}

task downloadZipFile(type: Download) {
    src 'https://download.scrolls.com/callersbane/server/CallersBane-Server.zip'
    dest new File(buildDir, 'server.zip')
}

task unzip(type: Copy) {
    from zipTree(downloadZipFile.dest)
    into "${buildDir}/server"
}

task moveJar(type: Copy) {
    from "${buildDir}/server/CallersBane-Server/Server.jar"
    into "lib"
    rename "Server.jar", "callersbane-server.jar"
}

task copyLibs(type: Copy) {
    from "${buildDir}/server/CallersBane-Server/lib/"
    include "*.jar"
    into "lib"
}

task cleanup(type: Delete) {
    delete "${buildDir}/server", "${buildDir}/server.zip"
}

task copyToLib(type: Copy) {
    into "$buildDir/libs/lib"
    //removes the included libraries that Caller's Bane has
    from(configurations.compile - fileTree("lib"))
}

task prepareWorkspace(type: GradleBuild) {
    tasks = ['downloadZipFile', 'unzip', 'moveJar', 'copyLibs', 'cleanup']
}

task generateRunScript() {
    doLast {
        new File("$buildDir/libs/startServer.sh").text = """java -Dio.netty.epollBugWorkaround=true -Dscrolls.mode=PROD -Dscrolls.node.id=test-server -cp "turtles-1.0-SNAPSHOT.jar:Server.jar:lib/*:cfg/:" org.wakingstones.turtles.core.Main"""
        new File("$buildDir/libs/startServer.bat").text = """java -Dio.netty.epollBugWorkaround=true -Dscrolls.mode=PROD -Dscrolls.node.id=test-server -cp "turtles-1.0-SNAPSHOT.jar;Server.jar;lib/*;cfg/;" org.wakingstones.turtles.core.Main"""
    }
}

jar {
    manifest {
        attributes(
                "Class-Path": "Server.jar lib/ cfg/",
                "Main-Class": "org.wakingstones.turtles.core.Main"
        )
    }
}

build.dependsOn(copyToLib, generateRunScript)